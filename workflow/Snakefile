import os
import sys
import re
import pandas as pd

## define config file
configfile: "config/config.yaml"

## define config dirs
base_filename = config["base_filename"]
bowtie_idx_ref_dir = config["bowtie_idx_ref_dir"]
pgRNA_counts_ref = config["pgRNA_counts_ref"]

#wildcard_constraints:
    ## note: regexp ^[^.]*$ means that dmx_read cannot contain a period in this rule
#    dmx_read = "^[^.]*$"

## set up paths for output files
results_dir_dict = {"fastq_dir": "input/fastqs/",
                    "fastqc_dir": "results/fastqc/",
                    "bowtie_idx_dir": "results/bowtie_index/",
                    "fastq_trimmed_dir": "results/fastq_trimmed/",
                    "fastq_demuxed_dir": "results/fastq_demuxed/",
                    "sam_dir": "results/sam/",
                    "bam_dir": "results/bam/",
                    "bam_sorted_dir": "results/bam_sorted/",
                    "pgRNA_counts_dir": "results/pgRNA_counts/"}

read_to_fastq = {}
fastq_trim_coords = {}

## convert to a dictionary too (so position isn't key)
trim_coords = [config["R1_coords"], config["R2_coords"], config["R3_coords"]]

fastq_list = [file for file in os.listdir(results_dir_dict["fastq_dir"]) if file != "README.txt"]
fastq_list.sort()

count = 0
for count, i in enumerate(fastq_list):
    key = "R" + str(count+1)
    read_to_fastq[key] = i
    fastq_trim_coords[key] = trim_coords[count]

print("fastq list: ", fastq_list)
print("fastq_dir: ", results_dir_dict["fastq_dir"])

sample_list = []
barcode_ref_file = open(config["barcode_ref_file"], "r")
# print("barcode_ref_file=", barcode_ref_file)
for line in barcode_ref_file:
    # print("line=", line)
    ## get sample names from idemp ref file
    sample = line.strip().split("\t")[1]
    # print("sample=", sample)
    sample_list.append(sample)
sample_list.sort()
barcode_ref_file.close()

print('sample list: ', sample_list)


rule all:
    input:
        ## rule fastqc
        # O
        expand(results_dir_dict["fastqc_dir"] + "{fastq}_html-out.html", fastq=fastq_list),
        expand(results_dir_dict["fastqc_dir"] + "{fastq}_zip-out.zip", fastq=fastq_list),
        
        ## rule trim_reads
        # I
        expand(results_dir_dict['fastq_dir'] +  "{fastq}", fastq=fastq_list),
        # O
        expand(results_dir_dict['fastq_trimmed_dir'] + base_filename +  "_{read}_trimmed.fastq", read=read_to_fastq),
        
        ## rule demux_fastqs
        # I
        expand(results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R3_trimmed.fastq"),
        expand(results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R1_trimmed.fastq"),
        expand(results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R2_trimmed.fastq"),
        # O
        expand(results_dir_dict["fastq_demuxed_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}.fastq.gz", dmx_read = ["R1", "R2"], sample = sample_list),
       
        ## rule: build_bowtie_index
        expand(results_dir_dict["bowtie_idx_dir"] + "pgPEN_{dmx_read}.{bt_idx}.ebwt",
            dmx_read = ["R1", "R2"],
            bt_idx = ["1", "2", "3", "4", "rev.1", "rev.2"]),
        
        ## rule: align_reads
        expand(results_dir_dict["sam_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned.sam",
            dmx_read = ["R1", "R2"],
            sample = sample_list),
        
        ## rule: make_sorted_bams
        expand(results_dir_dict["bam_sorted_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned_sorted.bam",
            dmx_read = ["R1", "R2"],
            sample = sample_list),
        
        ## rule: get_stats
        expand(results_dir_dict["bam_sorted_dir"] + "flagstat/" + base_filename + "_{dmx_read}_trimmed_{sample}_aligned_sorted_flagstat.txt",
            dmx_read = ["R1", "R2"],
            sample = sample_list),
        
        ## rule: count_pgRNAs
        expand(results_dir_dict["pgRNA_counts_dir"]+ "counts_{sample}.txt",
            sample = sample_list),
        
        ## rule: combine_counts
        results_dir_dict["pgRNA_counts_dir"] + base_filename + "_pgRNA_counts.txt"

rule fastqc:
    input:
       fastq = results_dir_dict["fastq_dir"] + "{fastq}"
    output:
        html = results_dir_dict["fastqc_dir"] + "{fastq}_html-out.html",
        zip = results_dir_dict["fastqc_dir"] + "{fastq}_zip-out.zip"
    log:
        "workflow/logs/fastqc/{fastq}.log"
    threads: 1
    wrapper:
        "v1.21.0/bio/fastqc"

rule trim_reads:
    input:
        expand(results_dir_dict['fastq_dir'] +  "{fastq}", fastq=fastq_list)
    output:
        results_dir_dict['fastq_trimmed_dir'] + base_filename + "_{read}_trimmed.fastq"
    conda:
        "envs/fastx_toolkit.yaml"
    params: ## is this the best way to do this?
        start = lambda wildcards: fastq_trim_coords[wildcards.read][0],
        end = lambda wildcards: fastq_trim_coords[wildcards.read][1]
    log:
        "workflow/logs/trim_fastqs/{read}.log"
    shell:
        "zcat {input} | fastx_trimmer -f {params.start} -l {params.end} -o {output} 2>{log}"

rule demux_fastqs:
    input:
        idx = results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R3_trimmed.fastq",
        R1 = results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R1_trimmed.fastq",
        R2 = results_dir_dict["fastq_trimmed_dir"] + base_filename + "_R2_trimmed.fastq"
    output:
        ## put the expand statement here so this is only run once
        ##   (instead of once per output file) - can't run w/o expand statement
        ##   because then the input & output don't have matching wildcards
        expand(results_dir_dict["fastq_demuxed_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}.fastq.gz", dmx_read = ["R1", "R2"], sample = sample_list)
    params:
        idemp = config["idemp"],
        n_mismatch = 1,
        ref = config["barcode_ref_file"],
        out_dir = results_dir_dict["fastq_demuxed_dir"], ## can't have folder as output => make param
        bad_names = expand(results_dir_dict["fastq_demuxed_dir"] + base_filename + "_{dmx_read}_trimmed.fastq_{sample}.fastq.gz",
            dmx_read = ["R1", "R2"],
            sample = sample_list) ## to get rid of dumb default idemp output names
    log:
        "workflow/logs/demux_fastqs/idemp.log"
    resources:
        mem = 98,
        time = 24,
        cpus_per_task = 16 ## must use Python underscores not Bash dashes
    shell:
        """
        {params.idemp} -b {params.ref} -n {params.n_mismatch} \
        -I1 {input.idx} -R1 {input.R1} -R2 {input.R2} -o {params.out_dir} &> {log}

        ## convert wildcards to bash arrays
        declare -a arr1=({params.bad_names})
        declare -a arr2=({output})
        len=${{#arr1[@]}} ## get length of arrays
        ## loop through wildcard arrays and move to correctly-named file
        for ((i=0; i<$len; i++));
        do
            # echo "${{arr1[$i]}}"
            mv ${{arr1[$i]}} ${{arr2[$i]}}
        done
        """      

rule build_bowtie_index:
    input:
        bowtie_idx_ref_dir + "pgPEN_{dmx_read}.fa"
    output:
        expand(results_dir_dict["bowtie_idx_dir"] + "pgPEN_{{dmx_read}}.{bt_idx}.ebwt",
            bt_idx = ["1", "2", "3", "4", "rev.1", "rev.2"])
    conda:
        "envs/bowtie.yaml"
    params:
        outfile_prefix = results_dir_dict["bowtie_idx_dir"] + "pgPEN_{dmx_read}"
    log:
        "workflow/logs/build_bowtie_index/{dmx_read}"
    shell:
        "bowtie-build -f {input} {params.outfile_prefix} &> {log}"

rule align_reads:
    input:
        fastq = results_dir_dict["fastq_demuxed_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}.fastq.gz"
    output:
        results_dir_dict["sam_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned.sam"
    conda:
        "envs/bowtie.yaml"
    params:
        idx = results_dir_dict["bowtie_idx_dir"] + "pgPEN_{dmx_read}"
    resources:
        mem = 98,
        time = 24,
        cpus_per_task = 16 ## must use Python underscores not Bash dashes
    log:
        "workflow/logs/align_reads/{dmx_read}_{sample}.log"
    shell:
        "bowtie -q -v 1 --best --strata --all --sam -p 4 {params.idx} {input.fastq} {output} &> {log}"

rule make_sorted_bams:
    input:
        results_dir_dict["sam_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned.sam"
    output:
        results_dir_dict["bam_sorted_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned_sorted.bam"
    conda:
        "envs/samtools.yaml"
    params:
        unsorted_bam_dir = results_dir_dict["bam_dir"],
        unsorted_bam = base_filename + "_{dmx_read}_trimmed_{sample}_aligned.bam",
        tmp = results_dir_dict["bam_sorted_dir"] + "tmp_{dmx_read}_{sample}"
    resources:
        mem = 32,
        time = 24
    log:
        "workflow/logs/make_sorted_bams/{dmx_read}_{sample}.log"
    shell:
        """
        mkdir -p {params.unsorted_bam_dir} ## TO DO: write a fxn to make this dir if it doesn't exist
        samtools view -bS -o {params.unsorted_bam_dir}/{params.unsorted_bam} {input}
        samtools sort -O bam -n {params.unsorted_bam_dir}/{params.unsorted_bam} -T {params.tmp} -o {output} &>{log}
        """

rule get_stats:
    input:
        results_dir_dict["bam_sorted_dir"] + base_filename + "_{dmx_read}_trimmed_{sample}_aligned_sorted.bam"
    output:
        results_dir_dict["bam_sorted_dir"] + "flagstat/" + base_filename + "_{dmx_read}_trimmed_{sample}_aligned_sorted_flagstat.txt"
    conda:
        "envs/samtools.yaml"
    log:
        "workflow/logs/get_stats/{dmx_read}_{sample}.log"
    shell:
        "samtools flagstat {input} 2>{log} 1>{output}" 

rule count_pgRNAs:
    input:
        results_dir_dict["bam_sorted_dir"] + base_filename + "_R1_trimmed_{sample}_aligned_sorted.bam"
    output:
        results_dir_dict["pgRNA_counts_dir"] + "counts_{sample}.txt"
    conda:
        "envs/counter_efficient.yaml"
    params:
        ref = config["pgRNA_counts_ref"], ## do I need to do os.path.abspath?
        n_chunks = config["n_chunks"], ## default 50 for HeLa
        out_dir = results_dir_dict["pgRNA_counts_dir"],
        script = "workflow/scripts/counter_efficient.R"
    resources:
        mem = 98,
        time = 24,
        cpus_per_task = 16 ## must use Python underscores not Bash dashes
    log:
        "workflow/logs/count_pgRNAs/{sample}.log"
    shell:
        "Rscript {params.script} {input} {params.n_chunks} {params.ref} {params.out_dir} &> {log}"

rule combine_counts:
    input:
        expand(results_dir_dict["pgRNA_counts_dir"] + "counts_{sample}.txt",
            sample = sample_list)
    output:
        results_dir_dict["pgRNA_counts_dir"] + base_filename + "_pgRNA_counts.txt"
    conda:
        "envs/python.yaml"
    params:
        annot_file = config["pgRNA_counts_ref"], ## do I need os.path.abspath?
        in_dir = results_dir_dict["pgRNA_counts_dir"],
        script = "workflow/scripts/combine_counts.py"
    log:
        "workflow/logs/combine_counts/combine_counts.log"
    shell:
        "python {params.script} {params.annot_file} {params.in_dir} {output} &>{log}"
        # ## read in pgRNA annotations to get IDs
        # annot_df = pd.read_csv(params.annot_file, sep = "\t") ## will this work??
        #
        # ## loop through files in pgRNA_counts_dir
        # dfs = [annot_df]
        # ## sorted/lambda stuff is to order files alphabetically
        # for file in sorted(os.scandir(params.in_dir), key=lambda e: e.name):
        #     df = pd.read_csv(file, sep = "\t")
        #     counts = df.loc[:, df.columns.str.contains("counts")] ## extract counts col
        #     dfs.append(counts)
        #
        # out_df = pd.concat(dfs, axis = 1)
        #
        # ## write to output file
        # out_df.to_csv(output[0], sep = "\t", index = False, header = True)